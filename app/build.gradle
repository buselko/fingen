repositories {
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'


android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.yoshione.fingen"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 132
        versionName "2.6.4.2"
        resValue "string", "app_name", "Fingen"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkReleaseBuilds false
    }

    signingConfigs { config }

    def keystorePropertiesFile = rootProject.file('keystore.properties')
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    if (keystorePropertiesFile.exists() &&
            keystoreProperties.containsKey('inAppLicenseKey') &&
            keystoreProperties.containsKey('inAppLicenseSalt') &&
            keystoreProperties.containsKey('dropboxApiKey') &&
            keystoreProperties.containsKey('mapsApiKey') &&
            keystoreProperties.containsKey('storeFile') && keystoreProperties['storeFile'] != "" &&
            keystoreProperties.containsKey('storePassword') &&
            keystoreProperties.containsKey('oAuthWebClientID') &&
            keystoreProperties.containsKey('keyAlias') &&
            keystoreProperties.containsKey('keyPassword')) {
        android.defaultConfig.resValue "string", "oAuthWebClientID", keystoreProperties["oAuthWebClientID"]
        android.defaultConfig.resValue "string", "inAppLicenseKey", keystoreProperties["inAppLicenseKey"]
        android.defaultConfig.resValue "string", "inAppLicenseSalt", keystoreProperties["inAppLicenseSalt"]
        android.defaultConfig.manifestPlaceholders = [
                dropboxApiKey: keystoreProperties["dropboxApiKey"],
                mapsApiKey   : keystoreProperties["mapsApiKey"]
        ]
        android.signingConfigs.config.storeFile = file(keystoreProperties['storeFile'])
        android.signingConfigs.config.storePassword = keystoreProperties['storePassword']
        android.signingConfigs.config.keyAlias = keystoreProperties['keyAlias']
        android.signingConfigs.config.keyPassword = keystoreProperties['keyPassword']
    } else {
        android.defaultConfig.resValue "string", "inAppLicenseKey", ""
        android.defaultConfig.resValue "string", "inAppLicenseSalt", ""
        android.defaultConfig.manifestPlaceholders = [
                dropboxApiKey: "",
                mapsApiKey   : ""
        ]
        android.buildTypes.release.signingConfig = null
    }
    if (keystorePropertiesFile.exists() &&
            keystoreProperties.containsKey('xVersion') &&
            keystoreProperties.containsKey('urlWebAPI'))
    {
        android.defaultConfig.versionName = android.defaultConfig.versionName.concat("-X." + keystoreProperties["xVersion"])
        android.defaultConfig.resValue "string", "urlWebAPI", keystoreProperties["urlWebAPI"]
        android.defaultConfig.resValue "string", "urlChangelogX", keystoreProperties["urlWebAPI"] + "changelog?version=" + keystoreProperties["xVersion"]
    } else {
        android.defaultConfig.resValue "string", "urlWebAPI", ""
        android.defaultConfig.resValue "string", "urlChangelogX", ""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            resValue "string", "app_name", "FG debug"
//            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions "main"
    productFlavors {
        prod {
            dimension "main"
        }
//        sync {
//            applicationIdSuffix ".sync"
//            resValue "string", "app_name", "Fingen sync"
//            dimension "main"
//        }
    }
    packagingOptions {
        resources {
            excludes += ['LICENSE.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.yoshione.fingen'

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        viewBinding true
    }
}


dependencies {
    repositories {
        mavenCentral()
    }
    implementation platform('com.google.firebase:firebase-bom:31.2.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.android.gms:play-services-auth:20.4.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "com.google.android.material:material:1.8.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/opencsv-2.4.jar')
    implementation files('libs/juniversalchardet-1.0.3.jar')
    implementation(name: 'MaterialShowCase-1.0.5', ext: 'aar')
    implementation(name: 'HeaderDecor-0.2.7', ext: 'aar')
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.1.0'
    implementation 'com.airbnb.android:airmapview:2.1.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "com.google.android.gms:play-services-maps:18.1.0"
    implementation "com.google.android.gms:play-services-vision:20.1.3"
    implementation 'com.dropbox.core:dropbox-core-sdk:5.4.4'
    implementation 'com.evernote:android-job:1.4.3'
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'de.mdxdave:changeloglib:-SNAPSHOT'
    implementation 'com.github.hotchemi:permissionsdispatcher:3.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.l4digital.fastscroll:fastscroll:1.0.4'
    implementation 'com.mikepenz:actionitembadge:4.0.0@aar'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'eu.davidea:flipview:1.1.3'
    implementation 'io.requery:sqlite-android:3.20.0'
    implementation 'net.xpece.android:support-preference:2.1.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'ru.tinkoff.decoro:decoro:1.5.1'
    implementation('com.anjlab.android.iab.v3:library:1.0.44') { exclude module: 'support-v4' }
    implementation('com.github.omadahealth:lollipin:2.1.0@aar') { transitive = true }
    implementation('com.mikepenz:materialdrawer:6.0.7@aar') { transitive = true }
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11'

    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"

    implementation "com.jakewharton:butterknife:10.2.3"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.3"

    implementation "com.google.dagger:dagger:2.45"
    annotationProcessor "com.google.dagger:dagger-compiler:2.45"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    implementation 'com.aurelhubert:ahbottomnavigation:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

}
apply plugin: 'com.google.gms.google-services'